//
// Created by intellij-pest on 2021-04-26
// LOOP main grammar as defined theoretical computer science and logic
// Author: bmahmoud
//

// Defaults
WHITESPACE = _{ " " | "\t" }

grammar = _{ SOI ~ terms ~ EOI }

// Terminals:
IDENT = @{ (ASCII_ALPHA | "_") ~ ASCII_ALPHANUMERIC* }

OP_PLUS = { "+" }
OP_MINUS = { "-" }
OP_MULTIPLY = { "*" }

SIMPLE_OPERATOR = _{ OP_PLUS | OP_MINUS }
EXT_OPERATOR = _{ OP_MULTIPLY }
OPERATOR = _{ SIMPLE_OPERATOR |  EXT_OPERATOR }

VALUE = @{ ASCII_DIGIT+ }
ZERO = @{ "0" }
SEP = _{ (";" | (";"? ~ NEWLINE+)) }

// Comparison Operators (needed for capture)
EQ = { "=" | "==" }
NE = { "!=" }
GT = { ">" }
GE = { ">=" }
LT = { "<" }
LE = { "<=" }

// Comparison Collection
compEqual = { IDENT ~ EQ ~ VALUE }
compNotEqual = { IDENT ~ NE ~ VALUE }
// currently != 0 has a special meaning
compNotEqual0 = { IDENT ~ NE ~ ZERO }
compGreaterEqual = { IDENT ~ GE ~ VALUE }
compGreaterThanIdent = { IDENT ~ GT  ~ IDENT }
compGreaterThan = { IDENT ~ GT  ~ VALUE }
compLessEqual = { IDENT ~ LE ~ VALUE }
compLessThan = { IDENT ~ LT  ~ VALUE }

comp = _{
    compEqual
    | compNotEqual
}

// Core Language:
binaryOp = {
    IDENT ~ SIMPLE_OPERATOR ~ VALUE
}

assign = {
    IDENT ~ ":=" ~
    binaryOp
}

loop_ = {
    "LOOP" ~ IDENT ~ "DO" ~ SEP
    ~ terms ~
    "END"
}

while_ = {
    "WHILE" ~ compNotEqual0 ~ "DO" ~ SEP
    ~ terms ~
    "END"
}

// macros
macroAssignToIdent = {
    IDENT ~ ":=" ~
    IDENT
}
macroAssignToZero =   {
    IDENT ~ ":=" ~
    "0"
}
macroAssignToValue = {
    IDENT ~ ":=" ~
    VALUE
}
macroAssignToIdentOpIdent = {
    IDENT ~ ":=" ~
    IDENT ~ OPERATOR ~ IDENT
}
macroAssignToIdentExtOpValue = {
    IDENT ~ ":=" ~
    IDENT ~ EXT_OPERATOR ~ VALUE
}

macroIf = {
    "IF" ~ compNotEqual0 ~ "THEN" ~ SEP
    ~ terms ~
    "END"
}
macroIfElse = {
    "IF" ~ compGreaterThanIdent ~ "THEN" ~ SEP
    ~ terms ~
    "ELSE" ~ SEP
    ~ terms ~
    "END"
}

macro_ = _{
    macroAssignToIdentExtOpValue
    | macroAssignToIdentOpIdent
    | macroAssignToIdent
    | macroAssignToZero
    | macroAssignToValue
    | macroIf
    | macroIfElse
}

// Expression Collection:
expression = _{
    loop_
    | while_
    | assign
    | macro_
}

terms = {
    NEWLINE* ~
    expression ~
    (SEP ~ expression)* ~
    SEP?
}